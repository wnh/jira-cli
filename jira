#!/usr/bin/env joker
(ns scripts.jira
  (:require [joker.string   :as s]
            [joker.json     :as j]
            [joker.os       :as os]
            [joker.filepath :as fp]
            [joker.http     :as h]
            [joker.base64   :as b64]))

(def *debug* (os/get-env "DEBUG"))
(def *jira-domain* (or (os/get-env "JIRA_DOMAIN") "taskhuman"))
(def *max-width* 80)
(def *eight* "    ")

(defn debug-pprint [& args]
  (if *debug*
    (apply pprint args)))

;; token should be in the form "username:password"
(defn token []
  (let [token-file (fp/join (os/get-env "HOME") ".atlassian_token")
        st (s/trim (slurp token-file))]
    (b64/encode-string (str "will@taskhuman.com:" st))))

;;; TEXT REFLOW

(defn count-after-add
  "how long will the last line be if we add the next word to it?"
  [line next-word]
  (+ (count line)
     (count next-word)
     ; for the space
     1))

(defn clean-prefix [prefix]
  (s/replace prefix #"\S" " "))

(defn reflow-lines [txt first-prefix width]
  (let [words (s/split txt #"\s+")
        indent (clean-prefix first-prefix)
        xlines (reduce (fn [[lines l] next-word]
                         (if (< width (count-after-add l next-word))
                           ;; The appending of " " leaves a trailing space on lines, eww
                           [(conj lines l) (str indent next-word " ")]
                           [lines (str l next-word " ")]))

                       [[] first-prefix]
                       words)
        pr-lines (conj (first xlines) (second xlines))]
    pr-lines))

;;; Content rendering for description


(defmulti render-content
  "Recursively transform issue content into terminal readable string based
  on the 'type'"
  (fn [c _]
    (get c "type")))

(defn render-children [children ctx]
  (map (fn [child] (render-content child ctx))
       children))

(defmethod render-content nil [data ctx]
  ; there is no content, seems to be a thing
  "")
(defmethod render-content "doc" [data ctx]
  (s/join "\n"
          (render-children (get data "content") ctx)))

(defmethod render-content "paragraph" [data ctx]
  ;(str ctx (s/join "  " (render-children (get data "content") ctx))))
  (let [ch (render-children (get data "content") ctx)
        block (s/join ch)
        ll (reflow-lines block ctx *max-width*)]
    (s/join "\n" ll)))

(defmethod render-content "text" [data ctx]
  (get data "text"))

(defmethod render-content "hardBreak" [data ctx]
  "\n")


(defmethod render-content "orderedList" [data ctx]
  (let [children (get data "content")
        indexes (map inc (range))]
    (->> children
         #_(map (fn [n child] (render-content child (str ctx (format "  %2d. " n)))) indexes)
         (map (fn [n child] (render-content child (str ctx "       "))) indexes)
         (s/join "\n"))

  ))

(defmethod render-content "bulletList" [data ctx]
  (s/join "\n" (render-children (get data "content")
                                (str ctx "  * "))))

(defmethod render-content "listItem" [data ctx]
  (s/join "\n" (render-children (get data "content") ctx)))

(defmethod render-content "mediaSingle" [data ctx]
  "TODO: media single"
  #_(prn data))
(defmethod render-content "inlineCard" [data ctx]
  (get-in data ["attrs" "url"])
  #_(str "<TODO inlineCard...>" (pr-str data)))
(defmethod render-content "link" [data ctx]
  "TODO: LINK HERE")
(defmethod render-content "codeBlock" [data ctx]
  "TODO: CODE HERE")
(defmethod render-content "blockquote" [data ctx]
  "TODO: blockquote")
(defmethod render-content "table" [data ctx]
  "TODO: blockquote")
(defmethod render-content "heading" [data ctx]
  (str "\n"
       ctx (->> "#" (repeat (get-in data ["attrs" "level"])) (apply str))
       " " (get-in data ["content" 0 "text"]))
  #_(pr-str "TODO: heading" data))

(defmethod render-content "mention" [data ctx]
  (let [name (get-in data ["attrs" "text"])
        out (if (s/includes? name " ")
              (str "[" name "]")
              name)]
    (str "@" out)))

(defmethod render-content "code" [data ctx]
  "TODO: CODE HERE")

(defn get-sprint
  "this is sketchy."
  [issue]
  (let [sprint-list (get-in issue ["fields" "customfield_10005"])
        sprint? (first sprint-list)]
    (get sprint? "name")))

(defn render-comment [cmt ctx]
  (println (str ctx
                (get-in cmt ["author" "displayName"])
                " on "
                (get-in cmt ["created"])))
  ;(prn(get-in cmt ["body" "content"]))
  (println (render-content (get-in cmt ["body"])
                           (str ctx "    "))))

(comment "
    MAIN-322
    Epic:   Workflow  ** how do we get this?
    Priority: High
    Assignee: Will Harding
    Labels: None
    Story: point estimate 5
    Sprint: Will's first sprint 2 [+1]
    Reporter: steve
    Development: Create branch
    ")

(defn render-attachments [issue prefix]
  (let [chunks (for [a (get-in issue ["fields" "attachment"])]
                 (str prefix
                      (get a "filename")
                      "\n"
                      prefix
                      "    "
                      (get a "content")))]
    (s/join "\n" chunks)))


(defn render-link [link]
  (if (link "outwardIssue")
    (println *eight*
             (format "%-10s [%s] %s   %s"
                     (get-in link ["outwardIssue" "key"])
                     (get-in link ["outwardIssue" "fields" "status" "statusCategory" "name"])
                     (get-in link ["type" "outward"])
                     (get-in link ["outwardIssue" "fields" "summary"]))))
  (if (link "inwardIssue")
    (println *eight*
             (format "%-10s [%s] %s   %s"
                     (get-in link ["inwardIssue" "key"])
                     (get-in link ["inwardIssue" "fields" "status" "statusCategory" "name"])
                     (get-in link ["type" "inward"])
                     (get-in link ["inwardIssue" "fields" "summary"])))))


(defn render-detail-view [issue]
  (println (get issue "key"))
  (println (s/join "\n" (reflow-lines  (get-in issue ["fields" "summary"])
                                       *eight*
                                       *max-width*)))
  (println)
  (println (str *eight* "https://" *jira-domain* ".atlassian.net/browse/" (get issue "key")))
  (println)

  (println "INFO")
  (println *eight* "Type:" (get-in issue ["fields" "issuetype" "name"]))
  (println *eight* "Priority:" (get-in issue ["fields" "priority" "name"]))
  (println *eight* "Assignee:" (get-in issue ["fields" "assignee" "displayName"]))
  (println *eight* "Reporter:" (get-in issue ["fields" "creator" "displayName"]))
  (if-let [sprint (get-sprint issue)]
    (println *eight* "Sprint:" sprint))
  (println)
  (if-let [att (render-attachments issue *eight*)]
    (do (println "ATTACHMENTS")
        (println att)))
  (println "LINKS")
  (doseq [link (get-in issue ["fields" "issuelinks"])]
    (render-link link))
  (println "")
  (println "DESCRIPTION")
  (println (render-content (get-in issue ["fields" "description"]) *eight*))

  (let [cmts (get-in issue ["fields" "comment" "comments"])]
    (if (< 0 (count cmts))
      (do (println "COMMENTS")
          (doseq [cmt cmts]
            (render-comment cmt *eight*))))))

(defn issue-view
  "Extract relevent keys from the raw Jira Issue"
  [i]
  ;;(pprint (get-in i ["fields" "description"]))
  {:key  (get-in i ["key"])
   :proj (get-in i ["fields" "project" "name"])
   :type (get-in i ["fields" "issuetype" "name"])
   :summary (get-in i ["fields" "summary"])
   :priority (get-in i ["fields" "priority" "name"])
   :status (get-in i ["fields" "status" "name"])
   :long-status (get-in i ["fields" "status" "name"])
   :text (render-content (get-in i ["fields" "description"]) "")
   ;:text (get-in i ["fields" "description" "content"])
   })

(def display-status
  {:done     "DONE"
   :backlog  "BKLG"
   :progress "INPRG"
   :pr       "PR"
   :bug-open "BOPEN"
   :bug-done "BDONE"})

(defn issue-display-short
  "Show single line issue descripition"
  [i]
  (let [[prj n] (s/split (:key i) "-")]
    (format "%12s %4s-%-5s  %s"
            (:status i)
            prj
            n
            (:summary i))))

(defn issue-display-long
  "Show full issue descripition with description"
  [i]
  (let [hdr (issue-display-short i)]
    (str hdr "\n" (:text i))))

(defn tk [c n]
  (take n c))

(defn mp [c f]
  (map f c))

(defn filter2 [c f]
  (filter f c))

(defn trace [x]
  (prn x)
  x)

(defn jira-req [q]
  (let [x (h/send
           {:url (str "https://" *jira-domain* ".atlassian.net/rest/api/3/search?jql=" q)
            :headers {"Authorization" (str "Basic " (token))
                      "Accept" "application/json"}})]
    x))

(defn jira-get [id]
  (h/send
   {:url (str "https://" *jira-domain* ".atlassian.net/rest/api/3/issue/" id)
    :headers {"Authorization" (str "Basic " (token))
              "Accept" "application/json"}}))
(defn priority-sort
  "Transform an issue to a syntetic numeric priority for sorting"
  [c]
  (let [order {"To Do" 0
               "In Progress" 2
               "Dev Complete" 3
               "Won't Do" 4}]
    (sort-by #(order (or (:status %) 20)) c)))

(defn not-done [x]
  (not= (:status x) "Done"))

(defn show-list []
  (let [display-fn (if (some #(= % "-l") *command-line-args*)
                     issue-display-long
                     issue-display-short)
        is (-> (jira-req "assignee=currentuser()")
               :body
               j/read-string
               (get "issues")
               ;(mp trace)
               (mp issue-view)
               (filter2 not-done)
               (priority-sort)
               (mp display-fn))]
    (doseq [i is]
      (println i))))

(defn jdump [x]
  (debug-pprint x)
  x)

(defn show-details [id]
  ;(println (format "Showing: %s" id))
  (-> (jira-get id)
      :body
      j/read-string
      jdump
      (render-detail-view)))

(defn main []
  (if (= 1 (count *command-line-args*))
    (show-details (first *command-line-args*))
    (show-list)))

(main)
